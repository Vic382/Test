#region Prolog

#***********************************************************************************************
# CREATEUR : VRT
# DATE DE CREATION : 23/11/2023
# DERNIERE MODIFICATION EFFECTUEE PAR : 
# DATE DE DERNIERE MODIFICATION : 
# DESCRIPTION DU PROCESSUS : Initialisation des taux de pénétration dans le cube Param_Taux_Penetration_Moyen_Transac
# Récupération du réel n-1 pour les moyen de paiement hors Ticket Resto et récupération du réel M-1 pour les Ticket Resto
# Le total moyen de transaction doit toujours être égal à 100%
# Donc si une augmentation ou diminution est saisie, on doit impacter les autres moyens de paiement en conséquence
#************************************************************************************************

###############################   MONITORING   #################################

### 0.Définition des variables du monitoring
Z_DEBUT_EXE =  StringToNumber(TimSt (Now, '\h'))*3600 +StringToNumber(TimSt (Now, '\i'))*60+StringToNumber(TimSt (Now, '\s'));
sCubeParamS	             = 'ParametresSources' ;
sCubeParamT	             = 'ParametrageT' ;
cMainProcName       = GetProcessName();
cRepertoireRacine   = GetProcessErrorFileDirectory;
cTimeStamp          = TimSt( Now, '\Y\m\d\h\i\s' );
cRandomInt          = NumberToString( INT( RAND( ) * 1000 ));
cTimeProcess        = cTimeStamp | '_' | cRandomInt;
sSource = '' ;
sNomFichier =  '' ;
nErrors = 0 ;
nErrorsSource = 0 ; 
zLogNumero         = 1 ;
nErrors          = 0;
vMessage              = '';

IF ( SUBST ( TM1User() , 1 , 2 ) @= 'R*' ) ;
	cUtilisateur = 'Admin' ;
ELSE ;
	cUtilisateur        = TM1User();
ENDIF ;

sDate = TimSt( Now, '\Y\m\d' );

sUser = ATTRS( '}Clients' , cUtilisateur , '}TM1_DefaultDisplayValue' );
nLong = LONG ( sUser ) ;
sUser = SUBST ( sUser , 20 , nLong - 19 ) ;

### 1. Déclaration des variables

# Pour suppression des vues et ss ensemble temporaire
nDebug = 1 ; 

#**Fichier Debug
sFichierRepDebug = CellGetS ( sCubeParamT , 'Txt_RepertoireDebug' , 'ValeurS' );
sDebug = sFichierRepDebug | 'test'|cMainProcName|'.csv' ;

#**Fichier Rejet
sFichierRepRejet = CellGetS ( sCubeParamT , 'Txt_RepertoireRejet' , 'ValeurS' );
sRejet = sFichierRepRejet | 'Rejet_'| '_' | sUser | '_' | sDate | '_' | cMainProcName|'.csv' ;

# Suppression du fichier de rejet si Existant
IF ( FileExists( sRejet ) = 1 ) ;
  ASCIIDelete( sRejet );
ENDIF ;


###############################   FIN MONITORING   #################################

### Déclaration des variables

## Compteurs
nMontantImpact = 0 ;

# Définition du périmètre
# La source est toujours le Réel N-1 et la cible est toujours le prev

# On récupère l'exercice en cours dans le cube paramètrefonctionnel
sExerciceSource = DimensionElementPrincipalName ( 'Exercice' , CellGetS( 'ParametresFonctionnels', 'ExerciceEnCours' , 'ValeurS' ));

# On récupère l'index de l'exercice en cours pour récupérer l'exercice précédent dans la dimension
nIndex = DIMIX( 'Exercice', sExerciceSource );
sExerciceSource = DIMNM( 'Exercice', nIndex -1 );

sScenarioSource = 'REEL' ;
sVersionSource = 'VF' ;

sScenarioCible = DimensionElementPrincipalName ( 'Scenario' , CellGetS( 'ParametresFonctionnels', 'ScenarioEnCours' , 'ValeurS' ));
sExerciceCible = DimensionElementPrincipalName ( 'Exercice' , CellGetS( 'ParametresFonctionnels', 'ExerciceEnCours' , 'ValeurS' ));
sVersionCible = DimensionElementPrincipalName ( 'Version' , CellGetS( 'ParametresFonctionnels', 'VersionEnCours' , 'ValeurS' ));

# Controle du scénario cible
# Si Réel, on ne fait rien
nErrorScenario = 0 ;

IF ( sScenarioCible @= 'REEL' ) ;
    nErrorScenario = 1 ;
    ProcessBreak;
ENDIF ;

###########################################
## Creation vue SOURCE sur Cout_Elec
###########################################

### Déclaration des variables
sCubeSource = 'Param_Taux_Penetration_Moyen_Transac';
sDimSource1 = 'Exercice';
sDimSource2 = 'Version' ; 
sDimSource3 = 'Scenario' ; 
sDimSource4 = 'Mois' ; 
sDimSource5 = 'Moyen_Transaction' ; 
sDimSource6 = 'Magasin' ; 
sDimSource = 'm_Param_Taux_Penetration_Moyen_Transac';

# Le nom de la vue du cube :
sVueSource = 'Source_vue_' | cMainProcName ; 

# Le nom des sous-ensembles
sSubsetSource = 'Source_Subset_' | cMainProcName ; 

# Création de la vue cible
IF( ViewExists( sCubeSource, sVueSource ) = 1 );
  ViewDestroy( sCubeSource, sVueSource );
ENDIF;
ViewCreate( sCubeSource, sVueSource , nDebug );

# Exercice
SubsetDestroy ( sDimSource1 , sSubsetSource ) ;
SubsetCreate ( sDimSource1 , sSubsetSource , nDebug ) ; 
SubsetElementInsert ( sDimSource1 , sSubsetSource , sExerciceSource , nDebug ) ;
ViewSubsetAssign ( sCubeSource , sVueSource , sDimSource1 , sSubsetSource ) ; 

# Version
SubsetDestroy ( sDimSource2 , sSubsetSource ) ;
SubsetCreate ( sDimSource2 , sSubsetSource , nDebug ) ; 
SubsetElementInsert ( sDimSource2 , sSubsetSource , sVersionSource , nDebug ) ;
ViewSubsetAssign ( sCubeSource , sVueSource , sDimSource2 , sSubsetSource ) ; 

# Scenario
SubsetDestroy ( sDimSource3 , sSubsetSource ) ;
SubsetCreate ( sDimSource3 , sSubsetSource , nDebug ) ; 
SubsetElementInsert ( sDimSource3 , sSubsetSource , sScenarioSource , nDebug ) ;
ViewSubsetAssign ( sCubeSource , sVueSource , sDimSource3 , sSubsetSource ) ; 

# Mois : si le scénario est budget alors tous les mois sinon que les mois prev
SubsetDestroy ( sDimSource4 , sSubsetSource ) ; 
IF ( sScenarioCible @= 'BUDGET' ) ;
  StringMDX = '{TM1FILTERBYLEVEL ( { TM1SUBSETALL ( [ '| sDimSource4 | ' ] ) } , 0 ) }' ;
  SubsetCreateByMDX ( sSubsetSource , StringMDX , nDebug  ) ;
ELSE ;

  SubsetCreate ( sDimSource4 , sSubsetSource , nDebug ) ; 

  # Récupération du 1er mois de simulation dans les attributs
  sFirstMoisPrev = CellGetS( '}ElementAttributes_Scenario', sScenarioCible , 'Premier_Mois_Prev'  );
  nFirstMoisPrev = DIMIX( 'Mois' , sFirstMoisPrev );

  # Boucle sur la dimension mois
  nTailleBoucle = DIMSIZ( sDimSource4 );
  i = 1 ;

  WHILE ( i <= nTailleBoucle ) ;

    sMois = DIMNM( sDimSource4 , i );
  
    # Contrôle avec l'index du 1er mois simulation
    IF ( ELLEV ( sDimSource4 , sMois ) = 0 & i >= nFirstMoisPrev ) ;
      SubsetElementInsert( sDimSource4 , sSubsetSource , sMois , 1 );
    ENDIF ;
  
    i = i + 1 ;
  
  END ;

ENDIF ;
ViewSubsetAssign ( sCubeSource , sVueSource , sDimSource4 , sSubsetSource ) ; 

# Moyen_Transaction : Tous les niv 0
SubsetDestroy ( sDimSource5 , sSubsetSource ) ; 
#StringMDX = '{TM1FILTERBYLEVEL ( { TM1SUBSETALL ( [ '| sDimSource5 | ' ] ) } , 0 ) }' ;
#SubsetCreateByMDX ( sSubsetSource , StringMDX , nDebug  ) ;
SubsetCreate ( sDimSource5 , sSubsetSource , nDebug ) ; 
SubsetElementInsert ( sDimSource5 , sSubsetSource , 'Total_Moyen_Paiement' , nDebug ) ;
ViewSubsetAssign ( sCubeSource , sVueSource , sDimSource5 , sSubsetSource ) ; 

# Magasin : Tous les niv 0
SubsetDestroy ( sDimSource6 , sSubsetSource ) ; 
StringMDX = '{TM1FILTERBYLEVEL ( { TM1SUBSETALL ( [ '| sDimSource6 | ' ] ) } , 0 ) }' ;
SubsetCreateByMDX ( sSubsetSource , StringMDX , nDebug  ) ;
ViewSubsetAssign ( sCubeSource , sVueSource , sDimSource6 , sSubsetSource ) ; 

# m_Param_Taux_Penetration_Moyen_Transac
SubsetDestroy ( sDimSource , sSubsetSource ) ; 
SubsetCreate ( sDimSource , sSubsetSource , nDebug ) ; 
SubsetElementInsert ( sDimSource , sSubsetSource , 'Taux_Penetration' , nDebug ) ;
ViewSubsetAssign ( sCubeSource , sVueSource , sDimSource , sSubsetSource ) ;

ViewExtractSkipCalcsSet( sCubeSource , sVueSource , 0 );
ViewExtractSkipRuleValuesSet( sCubeSource , sVueSource , 0 );
ViewExtractSkipZeroesSet( sCubeSource , sVueSource , 1 );

# Redéfinition de la source de donnée
DatasourceCubeview = sVueSource ;
DatasourceNameForServer = sCubeSource ;


###########################################
## Creation vue CIBLE sur Cout_Elec
###########################################

### Déclaration des variables
sCubeCible = 'Param_Taux_Penetration_Moyen_Transac';
sDimCible1 = 'Exercice';
sDimCible2 = 'Version' ; 
sDimCible3 = 'Scenario' ; 
sDimCible4 = 'Mois' ;
sDimCible5 = 'Moyen_Transaction' ; 
sDimCible6 = 'Magasin' ; 
sDimCible = 'm_Param_Taux_Penetration_Moyen_Transac';


# Le nom de la vue du cube :
sVueZero = 'RAZ_vue_' | cMainProcName ; 

# Le nom des sous-ensembles
sSubsetZero = 'RAZ_Subset_' | cMainProcName ; 

# Création de la vue cible
IF( ViewExists( sCubeCible, sVueZero ) = 1 );
  ViewDestroy( sCubeCible, sVueZero );
ENDIF;
ViewCreate( sCubeCible, sVueZero , nDebug );

# Exercice
SubsetDestroy ( sDimCible1 , sSubsetZero ) ;
SubsetCreate ( sDimCible1 , sSubsetZero , nDebug ) ; 
SubsetElementInsert ( sDimCible1 , sSubsetZero , sExerciceCible , nDebug ) ;
ViewSubsetAssign ( sCubeCible , sVueZero , sDimCible1 , sSubsetZero ) ; 

# Version
SubsetDestroy ( sDimCible2 , sSubsetZero ) ;
SubsetCreate ( sDimCible2 , sSubsetZero , nDebug ) ; 
SubsetElementInsert ( sDimCible2 , sSubsetZero , sVersionCible , nDebug ) ;
ViewSubsetAssign ( sCubeCible , sVueZero , sDimCible2 , sSubsetZero ) ; 

# Scenario
SubsetDestroy ( sDimCible3 , sSubsetZero ) ;
SubsetCreate ( sDimCible3 , sSubsetZero , nDebug ) ; 
SubsetElementInsert ( sDimCible3 , sSubsetZero , sScenarioCible , nDebug ) ;
ViewSubsetAssign ( sCubeCible , sVueZero , sDimCible3 , sSubsetZero ) ; 

# Mois : si le scénario est budget alors tous les mois sinon que les mois prev
SubsetDestroy ( sDimCible4 , sSubsetZero ) ; 
IF ( sScenarioCible @= 'BUDGET' ) ;
  StringMDX = '{TM1FILTERBYLEVEL ( { TM1SUBSETALL ( [ '| sDimCible4 | ' ] ) } , 0 ) }' ;
  SubsetCreateByMDX ( sSubsetZero , StringMDX , nDebug  ) ;
ELSE ;

  SubsetCreate ( sDimCible4 , sSubsetZero , nDebug ) ; 

  # Récupération du 1er mois de simulation dans les attributs
  sFirstMoisPrev = CellGetS( '}ElementAttributes_Scenario', sScenarioCible , 'Premier_Mois_Prev'  );
  nFirstMoisPrev = DIMIX( 'Mois' , sFirstMoisPrev );

  # Boucle sur la dimension mois
  nTailleBoucle = DIMSIZ( sDimCible4 );
  i = 1 ;

  WHILE ( i <= nTailleBoucle ) ;

    sMois = DIMNM( sDimCible4 , i );
  
    # Contrôle avec l'index du 1er mois simulation
    IF ( ELLEV ( sDimCible4 , sMois ) = 0 & i >= nFirstMoisPrev ) ;
      SubsetElementInsert( sDimCible4 , sSubsetZero , sMois , 1 );
    ENDIF ;
  
    i = i + 1 ;
  
  END ;

ENDIF ;

ViewSubsetAssign ( sCubeCible , sVueZero , sDimCible4 , sSubsetZero ) ; 

# Moyen_Transaction
SubsetDestroy ( sDimCible5 , sSubsetZero ) ; 
StringMDX = '{TM1FILTERBYLEVEL ( { TM1SUBSETALL ( [ '| sDimCible5 | ' ] ) } , 0 ) }' ;
SubsetCreateByMDX ( sSubsetZero , StringMDX , nDebug  ) ;
ViewSubsetAssign ( sCubeCible , sVueZero , sDimCible5 , sSubsetZero ) ; 

# Magasin
SubsetDestroy ( sDimCible6 , sSubsetZero ) ; 
StringMDX = '{TM1FILTERBYLEVEL ( { TM1SUBSETALL ( [ '| sDimCible6 | ' ] ) } , 0 ) }' ;
SubsetCreateByMDX ( sSubsetZero , StringMDX , nDebug  ) ;
ViewSubsetAssign ( sCubeCible , sVueZero , sDimCible6 , sSubsetZero ) ; 

# m_Param_Taux_Penetration_Moyen_Transac
SubsetDestroy ( sDimCible , sSubsetZero ) ; 
SubsetCreate ( sDimCible , sSubsetZero , nDebug ) ; 
SubsetElementInsert ( sDimCible , sSubsetZero , 'Taux_Penetration' , nDebug ) ;
ViewSubsetAssign ( sCubeCible , sVueZero , sDimCible , sSubsetZero ) ;

# RAZ de la vue zero
ViewZeroOut( sCubeCible, sVueZero );
#endregion
#region Data

# Initialisation d'un boucle pour récupérer les taux de variation de tous les moyens de paiement
i = 1 ;
nTailleBoucle = DimSiz ( 'Moyen_Transaction') ;

While ( i <= nTailleBoucle ) ;

  sMoyen_Transac = DIMNM( 'Moyen_Transaction' , i );
  
  IF ( ELLEV( 'Moyen_Transaction' , sMoyen_Transac ) = 0 );

    # Récupération du taux de variation ( augmentation en point ) à appliquer et des taux de pénétration réel
    nTaux = CellGetN( sCubeSource , sExerciceCible , sVersionCible , sScenarioCible , Mois , sMoyen_Transac , Magasin , 'Taux_Variation' ) ;
    
    # La valeur de référence est différente pour les tickets restos ( M-1 ), les autres moyens de paiement sont basé sur N-1
    IF ( sMoyen_Transac @= 'Ticket_Resto' ) ;
    
      # Récupération du mois précédent au mois parcouru
      sMoisPrec = CellGetS ( '}ElementAttributes_Mois', Mois , 'Mois_Precedent' ) ;
      
      # Définition de l'exercice source, si mois prec = Mars alors exercice N-1 sinon exercice en cours
      IF ( sMoisPrec @= 'M03' ) ;
        sExerciceTR = sExerciceSource ;
      ELSE ;
        sExerciceTR = sExerciceCible ;
      ENDIF ;
      
      nValeur = CellGetN( sCubeSource , sExerciceTR , sVersionSource , sScenarioSource , sMoisPrec , sMoyen_Transac , Magasin , 'Taux_Penetration' ) ;
      
    ELSE ;
    
      # Récupération pour les autres moyen de paiement
      nValeur = CellGetN( sCubeSource , sExerciceSource , sVersionSource , sScenarioSource , Mois , sMoyen_Transac , Magasin , 'Taux_Penetration' ) ;
      
    ENDIF ;
  
    # Si le taux est différent de 0 alors on alimente le taux de penetration avec la variation
    IF ( nTaux <> 0 ) ;
      nMontant = nValeur + nTaux ;
      CellPutN ( nMontant , sCubeCible , sExerciceCible , sVersionCible , sScenarioCible , Mois , sMoyen_Transac , Magasin , 'Taux_Penetration' ) ;
    # Sinon, incrémentation d'un compteur avec les valeurs des pénétration
    ELSE ;
      CellPutN ( nValeur , sCubeCible , sExerciceCible , sVersionCible , sScenarioCible , Mois , sMoyen_Transac , Magasin , 'Taux_Penetration' ) ;
      nMontantImpact = nMontantImpact + nValeur ;
    ENDIF ;
  ENDIF ;
  
  i = i + 1 ;
  
END ;

# Récupération du nouveau total après alimentation de la boucle
nTotal = CellGetN( sCubeSource , sExerciceCible , sVersionCible , sScenarioCible , Mois , 'Total_Moyen_Paiement' , Magasin , 'Taux_Penetration' );

# On calcul la différence entre 100% ( ancien total) et le nouveau total pour connaitre la variation à appliquer
nEcart = 1 - nTotal ;

# Initialisation d'une seconde boucle pour impacter les taux de pénétration en fonction des moyen de paiement qui ont été modifiés
j = 1 ;
nTailleBoucle2 = DimSiz ( 'Moyen_Transaction') ;

While ( j <= nTailleBoucle2 ) ;

  sMoyen_Transac = DIMNM( 'Moyen_Transaction' , j );
  
  IF ( ELLEV( 'Moyen_Transaction' , sMoyen_Transac ) = 0 );
  
    # Récupération du taux de variation ( augmentation en point ) à appliquer et des taux de pénétration réel
    nTaux = CellGetN( sCubeSource , sExerciceCible , sVersionCible , sScenarioCible , Mois , sMoyen_Transac , Magasin , 'Taux_Variation' );

    # La valeur de référence est différente pour les tickets restos ( M-1 ), les autres moyens de paiement sont basé sur N-1
    IF ( sMoyen_Transac @= 'Ticket_Resto' ) ;
    
      # Récupération du mois précédent au mois parcouru
      sMoisPrec = CellGetS ( '}ElementAttributes_Mois', Mois , 'Mois_Precedent' ) ;
      
      # Définition de l'exercice source, si mois prec = Mars alors exercice N-1 sinon exercice en cours
      IF ( sMoisPrec @= 'M03' ) ;
        sExerciceTR = sExerciceSource ;
      ELSE ;
        sExerciceTR = sExerciceCible ;
      ENDIF ;
      
      nValeur = CellGetN( sCubeSource , sExerciceTR , sVersionSource , sScenarioSource , sMoisPrec , sMoyen_Transac , Magasin , 'Taux_Penetration' ) ;
      
    ELSE ;
    
      # Récupération pour les autres moyen de paiement
      nValeur = CellGetN( sCubeSource , sExerciceSource , sVersionSource , sScenarioSource , Mois , sMoyen_Transac , Magasin , 'Taux_Penetration' ) ;
      
    ENDIF ;

    IF ( nTaux = 0 ) ;
  
      # Calcul par produit en croix de la proportion des taux de pénétration qui n'ont pas variés
      nProportion = ( nValeur * 1 ) \ nMontantImpact ;
    
      # On calcul le nouveau taux de pénétration : Valeur initiale + ( Ecart entre 1 et nouveau total * Proportion )
      nRepart = nEcart * nProportion ;
      nMontantRepart = nValeur + nRepart ;
    
      CellPutN ( nMontantRepart , sCubeCible , sExerciceCible , sVersionCible , sScenarioCible , Mois , sMoyen_Transac , Magasin , 'Taux_Penetration' ) ;
    
    ENDIF ;
  ENDIF ;
  
  j = j + 1 ;
  
END ;

# Remise à zero des compteurs
nMontantImpact = 0 ;
nProportion = 0 ;
nMontantRepart = 0 ;
#endregion
#region Epilog

#------------------------------------------------ MONITORING  ------------------------------------------------#

# Declaration variables et constantes

Z_FIN_EXE =  StringToNumber(TimSt (Now, '\h'))*3600 +StringToNumber(TimSt (Now, '\i'))*60+StringToNumber(TimSt (Now, '\s'));
Z_PERIODE=subst(TODAY(1),1,4) | subst(TODAY(1),6,2);

Z_STATUS=IF(GetProcessErrorFilename@='','OK','KO');
Z_TEMPS_EXECUTION= Z_FIN_EXE - Z_DEBUT_EXE;
# HEURE D'EXECUTION
nNewFormatId  =  NewDateFormatter ( 'fr-FR' , 'Europe/Paris' , 'serial' , 'medium' , 'datetime' );
Z_LOCAL_TIME = FormatDate ( NOW () , 'yyyy-MM-dd H:mm' , nNewFormatId );
Z_UTILISATEUR = TM1User;

nDebut= SCAN( 'pans:u:', Z_UTILISATEUR );
nDebutLong = LONG('pans:u:');
nStart = nDebut + nDebutLong ;
nTotal = LONG(Z_UTILISATEUR) ;
Z_UTILISATEUR = SUBST(Z_UTILISATEUR, nStart, nTotal - nStart - 1) ;

# Ecriture des informations sur le processus dans le cube ParametresSources                       

CELLPUTS( Z_STATUS , sCubeParamS , cMainProcName , 'Statut' );
CELLPUTN( Z_TEMPS_EXECUTION , sCubeParamS ,  cMainProcName ,'TempsExe');
CELLPUTS( Z_LOCAL_TIME, sCubeParamS , cMainProcName ,'DernExe');
CELLPUTS( Z_UTILISATEUR, sCubeParamS , cMainProcName ,'txt_Utilisateur' );

#------------------------------------------------ FIN - MONITORING  ------------------------------------------------

# Si le scénario n'est pas prévisionnel, on génère un message d'erreur
IF ( nErrorScenario <> 0 ) ;
    ItemReject( 'Le scénario sélectionné dans le cube Fonctionnel n' | CHAR ( 39 ) | 'est pas un scénario prévisionnel' ) ;
ENDIF ;



#endregion