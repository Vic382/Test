#region Prolog

#****Begin: Generated Statements***
#****End: Generated Statements****

#***********************************************************************************************************
# CREATEUR : DIMO SOFTWARE - BNS
# DATE DE CREATION : 09/12/2021
# DERNIERE MODIFICATION FFECTUEE PAR : 
# DATE DE DERNIERE MODIFICATION : 
# DESCRIPTION DU PROCESSUS : 
# Mise à jour de la sécurité pour la dimension magasin
#***********************************************************************************************************

#------------------------------------------------ MONITORING  ------------------------------------------------#

#***********************************************************************************************************
#                                  Définition des constantes
#***********************************************************************************************************

cMainProcName = GetProcessName();
cRepertoireRacine = GetProcessErrorFileDirectory;
cTimeStamp = TimSt( Now, '\Y\m\d\h\i\s' );
cRandomInt = NumberToString( INT( RAND( ) * 1000 ));
cTimeProcess = cTimeStamp | '_' | cRandomInt;
Z_DEBUT_EXE =  StringToNumber(TimSt (Now, '\h'))*3600 +StringToNumber(TimSt (Now, '\i'))*60+StringToNumber(TimSt (Now, '\s'));

IF ( SUBST ( TM1User() , 1 , 2 ) @= 'R*' ) ;
	cUtilisateur = 'Admin' ;
ELSE ;
	cUtilisateur        = TM1User();
ENDIF ;

#**********************************************************************************************************
#                               MAJ SECURITY USER GROUP
#**********************************************************************************************************

#***********************************************************************************************************
#                                  Définition des cubes
#***********************************************************************************************************

sCubeParamP = 'ParametresSources' ;
sCubeParamS = 'ParametresSources' ;
sCubeParamT = 'ParametrageT' ;

#------------------------------------------------ FIN - MONITORING  ------------------------------------------------#

##Définition des variables:

nDebug = 1 ;

##Définition du fichier source

# Récupération du fichier source
sNomFichier = CellGetS ( sCubeParamP , cMainProcName , 'Txt_NomFichier' ) ;
sRepertoire = CellGetS ( sCubeParamP , cMainProcName , 'Txt_RepertoireSource' ) ;
sNomFichier = sRepertoire | sNomFichier | '.csv' ;

# Contrôle du répertoire

IF ( sRepertoire @= '' % sNomFichier @= '' ) ;
  ItemReject( 'Le répertoire ou le nom du fichier n'| CHAR(39)|'est pas renseigné dans le cube ParamètreSources' );
  ProcessBreak;
ENDIF ;

# Controle de la présence du fichier source dans le répertoire
IF ( FileExists( sNomFichier ) = 0 ) ;
  ItemReject( 'Le fichier source ' | sNomFichier | ' n' | CHAR ( 39) | 'est pas présent dans le répertoire ' | sRepertoire );
  ProcessBreak;
ENDIF ;

DataSourceNameForServer = sNomFichier ;
DataSourceASCIIDelimiter = ';' ;

###########################################
## Creation vue zero sur MON CUBE
###########################################

###****Déclaration des variables

sCube = '}ElementSecurity_Poste_Budgetaire' ;
sDimPosteBud = 'Poste_Budgetaire' ;
sDimGroupe = '}Groups';

sVueZero = 'zTI_' | cMainProcName | '_Zero';

IF( ViewExists( sCube, sVueZero ) = 1 );
  ViewDestroy( sCube, sVueZero );
ENDIF;
ViewCreate( sCube, sVueZero , nDebug );

sDimension = 'Poste_Budgetaire' ;
IF( SubsetExists( sDimension , sVueZero ) = 1 );
  SubsetDestroy( sDimension , sVueZero );
ENDIF;
StringMDX = 'TM1SUBSETALL( ['|sDimension |'] )';
SubsetCreatebyMDX( sVueZero , StringMDX , nDebug );
ViewSubsetAssign( sCube, sVueZero , sDimension , sVueZero );

sDimension = '}Groups';
IF( SubsetExists( sDimension , sVueZero ) = 1 );
  SubsetDestroy( sDimension , sVueZero );
ENDIF;
StringMDX = 'EXCEPT ( {TM1FILTERBYLEVEL( {TM1SUBSETALL( [}Groups] )}, 0)}, { [}Groups].[ADMIN] , [}Groups].[SecurityAdmin] , [}Groups].[DataAdmin] , [}Groups].[OperationsAdmin] , [}Groups].[}tp_Everyone] } )';
SubsetCreatebyMDX( sVueZero , StringMDX , nDebug );
ViewSubsetAssign( sCube , sVueZero , sDimension , sVueZero );

ViewZeroOut( sCube, sVueZero );


#endregion
#region Metadata

#****Begin: Generated Statements***
#****End: Generated Statements****
#endregion
#region Data

#****Begin: Generated Statements***
#****End: Generated Statements****


##**Affectation des users par groupe.
IF ( ELLEV ( sDimPosteBud  , Code_Poste_Bud ) <> 0 ) ; 
  CellPutS ( 'READ' , sCube , Code_Poste_Bud , Groupe ) ;
ELSE;
  CellPutS ( Droit , sCube , Code_Poste_Bud , Groupe ) ;
ENDIF ;

nTailleBoucle = DIMSIZ ( sDimPosteBud ) ; 
While ( nTailleboucle > 0 ) ;
  sPoste_Bud = DIMNM ( sDimPosteBud  , nTailleBoucle )  ;
  nParent = ELISANC(  sDimPosteBud  , Code_Poste_Bud , sPoste_Bud ) ; 

  IF ( nParent = 1 )  ;
    IF ( ELLEV ( sDimPosteBud  , sPoste_Bud ) <> 0 ) ; 
      CellPutS ( 'READ' , sCube , sPoste_Bud , Groupe ) ;
    ELSE ; 
      CellPutS ( Droit , sCube , sPoste_Bud , Groupe ) ;
    ENDIF; 
  ENDIF ;
  nTailleBoucle = nTailleBoucle - 1 ; 
  # Fin des traitements de la boucle, on retourne au ‘’While’’ pour faire le test de condition	

End ;
#endregion
#region Epilog

#****Begin: Generated Statements***
#****End: Generated Statements****


#------------------------------------------------ MONITORING  ------------------------------------------------#
# Declaration variables et constantes
Z_FIN_EXE = StringToNumber(TimSt (Now, '\h'))*3600 +StringToNumber(TimSt (Now, '\i'))*60+StringToNumber(TimSt (Now, '\s'));
Z_PERIODE = Subst(TODAY(1),1,4) | subst(TODAY(1),6,2);
Z_STATUS = IF(GetProcessErrorFilename@='','OK','KO');
Z_TEMPS_EXECUTION = Z_FIN_EXE - Z_DEBUT_EXE;
Z_LOCAL_TIME =   today(1) | '  -  ' | subst(TIME,1,2) | 'h' | subst(TIME,4,2);
Z_UTILISATEUR = TM1User;

# Nettoyage -> CAMID("pans:u:cty@dimosoftware.com") into 'cty@dimosoftware.com'
nDebut= SCAN( 'pans:u:', Z_UTILISATEUR );
nDebutLong = LONG('pans:u:');
nStart = nDebut + nDebutLong ;
nTotal = LONG(Z_UTILISATEUR) ;
sUser = SUBST(Z_UTILISATEUR, nStart, nTotal - nStart - 1) ;

# Ecriture des informations sur le processus dans le cube ParametresSources                       
CELLPUTS( Z_STATUS , sCubeParamS , cMainProcName , 'Statut' );
CELLPUTN( Z_TEMPS_EXECUTION , sCubeParamS ,  cMainProcName ,'TempsExe');
CELLPUTS( Z_LOCAL_TIME, sCubeParamS , cMainProcName ,'DernExe');
CELLPUTS( sUser, sCubeParamS , cMainProcName ,'txt_Utilisateur' );
#CELLPUTS( Mois, sCubeParamS , cMainProcName ,'DernRef' );
#------------------------------------------------ FIN - MONITORING  -------------------------------------------#
#endregion